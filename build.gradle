plugins {
  id 'application'
  id 'java'
  id 'eclipse'
  id 'com.github.ben-manes.versions' version '0.51.0'
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.slf4j:slf4j-api:2.0.13'
  runtimeOnly 'ch.qos.logback:logback-classic:1.5.6'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}

application {
  mainClass = 'agebe.Main'
}

tasks.named('test') {
  // Use JUnit Platform for unit tests.
  useJUnitPlatform()
}

def isNonStable = { String version ->
  // added -JRE for guava
  def stableKeyword = ['RELEASE', 'FINAL', 'GA', '-JRE'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
  rejectVersionIf {
    isNonStable(it.candidate.version)
  }
}
